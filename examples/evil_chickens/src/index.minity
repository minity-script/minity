namespace evil_chickens

bossbar add chickens "Evil Chickens"
bossbar add timer "Time Left"

?max_chickens = 40
?init_chickens = 6
?chicken_time = 6
?max_time = 120
?blink_time = 20
?passenger = endermite


var $running = false
var $chickens = 0
var $time = 0
score death
score played
score chickens_killed

tag playing 
tag evil

stop()

function start() {
  unless $running intro()
} 

function intro() {
  tag @player[distance<10] playing
 
  after 1s {
    for @player.playing {
      / playsound minecraft:entity.lightning_bolt.thunder player {@s}
      / effect give {@s} minecraft:blindness 1 1 false
    }
  } then after 3s {
    for @player.playing {
      / playsound minecraft:entity.lightning_bolt.thunder player {@s}
      / effect give {@s} minecraft:blindness 1 1 false
    }
  } then after 2s {
    / title {@player.playing} title {=<t>The Chickens are Coming!</t>}
    / title {@player.playing} subtitle {=<t>You have {?max_time} seconds</t>}
  } then after 4s {
    for @player.playing {
      / playsound minecraft:entity.lightning_bolt.thunder player {@s}
      / playsound minecraft:entity.lightning_bolt.thunder player {@s}
      / effect give {@s} minecraft:blindness {?max_time} 120 true
    }
  } then after 1s start_game()
}

function start_game() {
  for @player.playing begin()

  $chickens = 0
  bossbar chickens players = @a.playing
  bossbar chickens visible = true
  bossbar chickens max = ?max_chickens
  bossbar chickens value = $chickens

  $time = ?max_time
  bossbar timer players = @a.playing
  bossbar timer visible = true
  bossbar timer max = ?max_time
  bossbar timer value = ?max_time
  $running = 1

  every 2t {
    if(@player.playing[->death>0]) defeat()
  } while $running


  var $count = 0
  every 3s {
    at any @player.playing summon_chicken()
    $count ++
  } while $running and while $count<?init_chickens then every 10s {
    at any @player.playing back 10 {
      summon_chicken()
    }
    $count ++
  } while $running and while $count<?max_chickens 

  tag lunging
  every 4s {
    as (random 1 @chicken.evil{OnGround:1b}) {
      @s::Motion[1] = 1d
      tag @s lunging
      after 4t {
        for @e.lunging {
          facing nearest @player.playing[distance>10] forward 10 {
            if (air) /tp ~ ~ ~
          }
          untag @s lunging
        }
      }
    } 
    

    as @e[type=?passenger].evil {
      @s::Lifetime = 0
    }

    for @chicken.evil unless (@player.playing[distance<20]) {
      up 1 facing furthest @player.playing forward 10 {
        if (air) /tp @s ~ ~ ~ facing entity {furthest @player.playing} eyes
      } 
      
      at furthest @player.playing[distance>50] {
        /tp ~ 10 ~
      }
    }
  } while ($running)

  every 1s {
    $time -- 
    bossbar timer value = $time
    if ($time < ?blink_time ) {
      bossbar timer visible = true
      for @player.playing {
        /playsound minecraft:entity.arrow.hit_player player {@s}
      }
      after 0.5s {
        bossbar timer visible = false
      }
    }
    if ($time<1) and if ($running>0) out_of_time()
  } while ($running) 
}

function chicken_killed() {
  for @e[type=?passenger].evil {
    as @s{OnGround:1b} /kill @s
    unless @chicken.evil[distance<0.5] /kill @s
  }
  $chickens --
  bossbar chickens value = $chickens  
  if ($chickens < 1 ) {
    victory()
  }
}

function stop() {
  $running = 0

  as @chicken.evil {
    @s::Motion[1] = 1d
    @s::Motion[1] = 1d
  }
  /effect clear {@player.playing} minecraft:blindness
  /kill {@e.evil}
  
  bossbar chickens visible = false
  bossbar timer visible = false
  untag @a.playing playing
}

function victory() {
  as @a.playing {
    /title @s title {=<t>Victory!</t>}
    /title @s subtitle {=<t>Evil chickens are vanquished.</t>}
  }
  stop()
}

function defeat() {
  /title {@a.playing} title {=<t>Defeat!</t>}
  /title {@a.playing} subtitle {=<t>Evil chickens got the best of you.\nBetter luck next time!</t>}
  stop()
}

function out_of_time() {
  /title {@a.playing} title {=<t>Out of time!</t>}
  /title {@a.playing} subtitle {=<t>Evil chickens will be back!</t>}
  stop()
}

score death deathCount

function begin() {
  tag @s playing
  @s->death = 0
  @s->played ++
  @s->chickens_killed = 0
}


function summon_chicken() {
  if ($running) {
    up 30 summon chicken{
      DeathLootTable:"minecraft:empty",
      CustomName: json <red>Evil Chicken</red>,
      Glowing: true,
      Attributes: [{Base:0.35d, Name:"minecraft:generic.movement_speed"}],
      Passengers:[{
        CustomName: json <green>Evil Chicken</green>,
        DeathLootTable:"minecraft:empty",
        id:"minecraft:{?passenger}",
        Tags:["{.evil}"],
        Invulnerable: true,
        Silent: true,
      }],
    } then {
      $chickens ++
      bossbar chickens value = $chickens  
      /effect give {@s} minecraft:resistance {?max_time} 2 true
      /effect give {@e[type=?passenger]} minecraft:invisibility {?max_time} 1 true
      tag @s evil
      /spreadplayers ~ ~ 5 30 false {@s}
    }
  }
}

on player_killed_entity{
  entity: {
    type: "minecraft:chicken",
    nbt: snbt {Tags:["{.evil}"]}
  },
  player: {
    nbt: snbt {Tags:["{.playing}"]}
  }
} then {
  @s->chickens_killed++
  print @a.playing I killed one, that's {@s->chickens_killed} so far
  chicken_killed()
}

on entity_killed_player{
  entity: {
    nbt: snbt {Tags:["{.evil}"]}
  }, 
  player: {
    nbt: snbt {Tags:["{.playing}"]}
  }
} then {
  print @a.playing I got killed
  defeat()
}
