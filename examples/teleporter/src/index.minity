namespace teleporter
import "./throwables"

tag teleporter

function teleporter #throwables:dropped  {
  var $at_pos
  tag teleport_now
  if (@s.teleporter) {
    if (@s::Item.tag.OldPosition) {
      @@OldPosition = @s::Item.tag.OldPosition
      @@OldRotation = @s::Item.tag.OldRotation
      $at_pos = @s::Item.tag.CurrentState
      if ($at_pos == 1) {
        @s::Item.tag.CurrentState = 2b
      } else {
        @s::Item.tag.CurrentState = 1b
      }
      tag @s teleport_now
      when throwables:find_owner then {
        say I dropped it again
        @@CurrentPosition = @s::Pos
        @@CurrentRotation = @s::Rotation
        tag @s teleport_now
        at @s summon marker then {
          @s::Pos = @@OldPosition
          @s::Rotation = @@OldRotation
          /tp {@e.teleport_now} @s
          untag @e.teleport_now teleport_now
          /kill @s
        } 
        /title @s title ""
        /title @s subtitle {=<t>Teleport position {$at_pos}</t>}
      } catch {
        /kill @s
      }
      @s::Item.tag.OldPosition = @@CurrentPosition
      @s::Item.tag.OldRotation = @@CurrentRotation
    } else {
      when throwables:find_owner then {
        say I dropped it
        /title @s title ""
        /title @s subtitle "Teleport position 2"
        @@OldPosition = @s::Pos
        @@OldRotation = @s::Rotation
      } catch {
        /kill @s
      }
      @s::Item.tag.CurrentState = 1b
      @s::Item.tag.OldPosition = @@OldPosition
      @s::Item.tag.OldRotation = @@OldRotation
    }
  }
}

/advancement revoke @a only teleporter:teleporter_made 
/recipe take @a teleporter:teleporter

function give_teleporter() {
  /recipe take @s teleporter:teleporter
  /advancement revoke @s only teleporter:teleporter_made 
  throwables:give_throwable(slime_ball,"{.teleporter}","Throwable Teleporter") 
  clear @s 1 slime_spawn_egg
}


function master #minity:master {
   give_teleporter()
   give_book()
}

define advancements/teleporter_made {
  criteria: {
    unlocked: {
      trigger: "minecraft:recipe_unlocked",
      conditions: {
        recipe: "teleporter:teleporter"
      }
    }
  },
  rewards: {
    "function": "teleporter:give_teleporter"
  }
}

define recipes/teleporter {
  type: "minecraft:crafting_shaped",
  pattern: [
    "SSS",
    "ScS",
    "SSS"
  ],
  key: {
    c: { item: "minecraft:compass" },
    S: { item: "minecraft:slime_block" }
  },
  result: { item: "minecraft:slime_spawn_egg", count: 1 }
}



function give_book() {
  give @s written_book{
    pages: [
      json <div> 
        <h>ON SLIME</h>
        <p>
          It has been long known that slime has interesting properties. 
          It is used daily in everything from trampolining to equestrianism.
        </p>
        <p>
          An ancient source claimed that a long distance traveling device could be ...
        </p>
      </div>,
      json <div> 
        <p>
          ... crafted from a <dark_red>compass fully enclosed in slime blocks</dark_red>,
          but since nobody at the time knew what a compass was, this was largely 
          ignored by contemporaries.
        </p>
        </div>,
      json <div>
        <p>
          This is an example for Minity, a scripting language for Vanilla Minecraft.
        </p>
        <p>
          <u 
            color = blue 

            text ="minity-script.github.io"
            clickEvent= {
              action: open_url,
              value: "https://minity-script.github.io/minity/#/"
            }
          />
        </p>
      </div>
    ],
    title:"On Slime",
    author:"Anonymous",
    display:{
      Name: json <t>On Slime</t>,
      Lore: [
        "A part of the Ancient Wisdoms series"
      ]
    }
  }
}
