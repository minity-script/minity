namespace use_thrown
import "./throwables"

tag sand_builder
tag sand_eater
tag sand_zapper
tag sand_looper

function master #minity:master {
  throwables:give_throwable(music_disc_cat,"{.sand_builder}","Sand Builder")
  throwables:give_throwable(music_disc_13,"{.sand_eater}","Sand Eater")
  throwables:give_throwable(music_disc_blocks,"{.sand_zapper}","Sand Zapper")
  throwables:give_throwable(music_disc_pigstep,"{.sand_looper}","Sand Looper")
}

function sand_builder #throwables:dropped  {
  if (@s.sand_builder) {
    when throwables:find_block then {
      repeat up 1 until air then {
        setblock sand
      }
    }
  }
}

function sand_zapper #throwables:dropped  {
  if (@s.sand_zapper) {
    when throwables:find_block then {
      if sand {
        repeat up 1 {
          setblock (down 1) air
        } while sand
      }
    }
  }
}


function sand_eater #throwables:dropped  {
  if (@s.sand_eater) {
    when throwables:find_block then {
      if sand summon marker then {
        tag @s sand_eater
      }
    }
  }
}

function sand_looper #throwables:dropped  {
  if (@s.sand_looper) {
    when throwables:find_block then {
      if sand summon marker then {
        tag @s sand_looper
      }
    }
  }
}

every 20t {
  for @marker.sand_eater {
    if (sand) {
      setblock air 
    } else {
      /kill @s
    }
  }
  for @marker.sand_looper {
    if (sand) {
      repeat up 1 until air then {
        setblock sand
      }
      setblock air
    } else {
      /kill @s
    }
  }
}

